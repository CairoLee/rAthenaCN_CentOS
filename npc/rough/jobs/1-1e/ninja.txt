//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//= 1.7 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//============================================================ 

alberta,30,65,3	script	赤城	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[船长]";
			mes "嗯...";
			mes "你必须来一下,";
			mes "感觉到某人";
			mes "正在这里等着你.";
			mes "告诉我, 你找到";
			mes "耐心的路径吗?";
			next;
			if(select("否:是") == 1) {
				mes "[船长]";
				mes "我看到.";
				mes "每个人都有自我,";
				mes "我认为. 照顾";
				mes "好你自己.";
				close;
			}
			mes "[船长]";
			mes "非常好.";
			mes "那么, 让我";
			mes "让我帮你";
			mes "走上正确道路...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		} else {
			mes "[船长]";
			mes "Hm? 我不能";
			mes "不服务";
			mes "你直到你成长得";
			mes "更强壮一点...";
			close;
		}
	} else {
		mes "[船长]";
		mes "Hmm...";
		mes "你和我...";
		mes "我们是水平相当的";
		mes "作战能力方面.";
		mes "也许我们可以讨论";
		mes "在一起的时候.";
		close;
	}
}

que_ng,30,65,3	script	克尔加#nq	730,{
	if (Upper == 2) {
		mes "[美洲狮]";
		mes "我... 我从来没有";
		mes "看到过孩子";
		mes "向你一样强壮!";
		mes "走开, 你";
		mes "大自然的怪物!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[美洲狮]";
			mes "嗯? 你来这里";
			mes "学习如何成为一个忍者?";
			mes "你经验不是很";
			mes "足够啊, 所以回去";
			mes "直到你更熟悉如何";
			mes "和怪物战斗.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "不好意思.";
			mes "你好?";
			next;
			mes "[克尔加]";
			mes "...............................";
			mes "你是怎么做到的?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "做什么?我没有";
			mes "做任何事情, 我想我没有...";
			next;
			mes "[克尔加]";
			mes "你怎么可以";
			mes "看到我? 我应该是";
			mes "肉眼看不见的.";
			mes "啊, 我现在知道了. 野猫 乔";
			mes "一定让你来杀我! 我不会上你的当! 去死!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "等一下!我都不";
			mes "知道谁是野猫 乔!";
			mes "冷静下来, 没有";
			mes "必要使用暴力!";
			next;
			mes "[克尔加]";
			mes "什么...?";
			mes "你怎么躲开";
			mes "我所有的攻击?";
			mes "你有一点天赋,";
			mes "我会教给你.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "我来到这里希望";
			mes "改变我的职业";
			mes "成为一个忍者.";
			next;
			mes "[克尔加]";
			mes "...哦. 就是这样?";
			mes "Hmm,你有巨大的";
			mes "潜力, 但我帮不了";
			mes "你现在. 我有太多的";
			mes "敌人, 我不能";
			mes "放松警惕,哪怕一秒钟.";
			next;
			mes "[克尔加]";
			mes "那野猫 乔 是";
			mes "冷血无情的...!";
			mes "他可以在任何时候出现!";
			mes "他会随时做任何事情";
			mes "来击败他的敌人!";
			next;
			mes "[克尔加]";
			mes "等一下,等一下, 我只是";
			mes "想到一些事情.";
			mes "也许你可以帮助我.";
			mes "我说你做, 我会教";
			mes "你一些技能如果你";
			mes "真的想当一名忍者.";
			next;
			if(select("- 当然.:- 不,谢谢.") == 2) {
				mes "[克尔加]";
				mes "嗯? 好的, 非常好.";
				mes "我依然不明白";
				mes "为什么我们不能";
				mes "彼此帮助";
				mes "在这个小小的困境里...";
				close;
			}
			mes "[克尔加]";
			mes "很好! 现在, 我想";
			mes "问 野猫 乔 是否愿意";
			mes "同意暂时休战.";
			mes "我知道我们两个";
			mes "没有武器, 所以我们";
			mes "应该先拿到好的武器.";
			next;
			mes "[克尔加]";
			mes "请把这封信,";
			mes "交给 Wildcat";
			mes "乔 在 艾音布罗克. 他是一个";
			mes "伪装高手, 所以请细心";
			mes "用眼睛找出他. 啊, ";
			mes "在高处寻找他.";
			next;
			mes "[克尔加]";
			mes "是的, 野猫 乔";
			mes "总有一个东西";
			mes "藏在高处.";
			mes "无论如何, 你把信给他";
			mes "之后, 回来然后";
			mes "让我知道他的答复.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[克尔加]";
			mes "即使这个任务";
			mes "不是那么迫切,";
			mes "请尽快到达";
			mes "艾音布罗克 和完成";
			mes "我给野猫 乔的信.";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[克尔加]";
			mes "你有没有交";
			mes "信给野猫 乔?";
			mes "我还需要知道他";
			mes "怎么回应我";
			mes "休战的建议. 无论如何, 看吧";
			mes "如果你能针对这一点.";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[克尔加]";
			mes "啊, 你已经回来了.";
			mes "野猫 乔 ";
			mes "有连同他的答复";
			mes "把你送回来吗? 很好, 很好,";
			mes "让我读一下.";
			next;
			mes "[克尔加]";
			mes "什么...?! 他怎么可以";
			mes "拒绝我的";
			mes "休战建议?!这只能表示";
			mes "他在制造另一个";
			mes "苦无. Nuts! 我必须";
			mes "赶上他, 否则我就是一个死人!";
			next;
			mes "[克尔加]";
			mes "听着, 你必须";
			mes "再一次帮助我! 我需要你";
			mes "收集一些材料";
			mes "我可以制作我自己的苦无和 野猫 乔战斗. 那么, 我将继续";
			mes "帮助你变更职业成为忍者.";
			next;
			mes "[克尔加]";
			mes "这些你需要";
			mes "带给我的";
			mes "^3355FF5 赛依伐^000000 和";
			mes "^3355FF1 强化武器金属-级数一^000000.";
			mes "请尽快";
			mes "拿到这些材料!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "嗯? 真有趣,";
			mes "野猫 乔 也在";
			mes "让我收集";
			mes "那些相同的材料.";
			next;
			mes "[克尔加]";
			mes "该死! 这意味着...";
			mes "你也在帮助 乔";
			mes "制作他的苦无! 不!";
			mes "我早就应该想到";
			mes "这一点的! 很好,现在已经";
			mes "太迟了. 只能尽快了!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[克尔加]";
				mes "尽快带回来";
				mes "^3355FF5 赛依伐^000000 和";
				mes "^3355FF1 强化武器金属-级数一^000000 给我,";
				mes "我才可以制造";
				mes "我自己的 苦无和";
				mes "野猫 乔对抗!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[克尔加]";
				mes "哇,哇...";
				mes "你还有一些";
				mes "剩余的技能点.";
				mes "你最好在转职前";
				mes "先把它们全部";
				mes "都使用, 好吗?";
				close;
			}
			mes "[克尔加]";
			mes "啊, 你把所有我需要";
			mes "的东西都带回来了.";
			mes "你比我预期的";
			mes "要早, 嗯? 非常好,";
			mes "正如所承诺的, 我会把你";
			mes "转职成为忍者.";
			next;
			mes "[克尔加]";
			mes "让我正式介绍";
			mes "我自己. 我是Touga忍者队里的";
			mes "高级忍者, 然后";
			mes "我是负责寻找卡兹玛";
			mes "的搜索队.";
			next;
			mes "[克尔加]";
			mes "卡兹玛是我村庄的";
			mes "一名小偷, 但他";
			mes "逃跑了.这导致了我们";
			mes "忍者队伍";
			mes "有了冲突.现在事情";
			mes "很麻烦...";
			next;
			mes "[克尔加]";
			mes "我一开始并不接受";
			mes "像你这样的忍者,因为";
			mes "现在情况很复杂.";
			mes "尽管如此, 你也证明了";
			mes "你是真正值得加入";
			mes "忍者队伍的.";
			next;
			mes "[克尔加]";
			mes "根据他的来信, 尽管";
			mes "乔对你的评价很高. 但";
			mes "要记住, 作为一名忍者, 你的";
			mes "任务是你的最优先级的. 但不要让任务目标";
			mes "取代了你的良心.";
			next;
			mes "[克尔加]";
			mes "''保密高于一切.''";
			mes "保证我们的秘密处于黑暗之中";
			mes "你可以在商人那里";
			mes "购买或者卖忍者的武器";
			mes "请记得";
			mes "保持警惕.";
			next;
			mes "[克尔加]";
			mes "今天, 你已经";
			mes "成为骄傲的忍者军团中";
			mes "一名忍者. 变成";
			mes "像风一样灵活, ";
			mes "像暗影一样安静.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // 赛依伐
			delitem 1010,1; // 强化武器金属-级数一
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[克尔加]";
			mes "你怎么了?";
			mes "刻苦训练:你希望";
			mes "能消失得";
			mes "无影无踪. 如果你";
			mes "能做到, 你能得到";
			mes "其他忍者的尊重~";
			close;
		}
	} else {
		if (BaseClass == Job_Ninja) {
			mes "[克尔加]";
			mes "你怎么了?";
			mes "刻苦训练: 你希望";
			mes "能消失得";
			mes "无影无踪. 如果你";
			mes "能做到, 你能得到";
			mes "其他忍者的尊重~";
			close;
		} else {
			mes "[克尔加]";
			mes "什么...?";
			mes "你怎么能够";
			mes "发现我隐藏";
			mes "在阴影中?!";
			mes "你肯定是";
			mes "非一般的冒险家, 嗯?";
			close;
		}
	}
}

einbroch,184,194,3	script	可疑男子#nq	 	881,{
	if (NINJ_Q == 1) {
		mes "[可以的人]";
		mes "我以前去过很多";
		mes "国家, 但我从来没有";
		mes "去过一个像艾音布罗克塔这么高";
		mes "的建筑. 所有";
		mes "在我家乡的建筑";
		mes "都比这里的要渺小...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "哦, 你来自";
		mes "Amatsu? 我在寻找";
		mes "某个叫";
		mes "野猫 乔的人.";
		next;
		mes "[可疑的人]";
		mes "...不. 不, 我真的是从";
		mes "依斯鲁得岛来的, 我只是";
		mes "为了艾音布罗克的一些矿物质";
		mes "而来. 告诉我, 你为什么";
		mes "寻找这个叫野猫 乔的人?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "好吧, 我需要把";
		mes "这封信送个他然后";
		mes "得到他的答复,那样";
		mes "我才能成为一个忍者.";
		next;
		mes "[可疑的人]";
		mes "真的?那现在我";
		mes "想一下,我想我曾经遇到";
		mes "他一次或者两次";
		mes "在这个镇上. 所以, 他更可能被叫做''红豹 乔,''";
		mes "而不是 ''野猫 乔.''";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我真的很希望能帮助你";
		mes "找到他, 但首先我必须";
		mes "找到我想找到的";
		mes "矿物质. 如果你不";
		mes "介意, 你能帮助我吗?";
		mes "那么我可以帮助你找到乔.";
		next;
		if(select("- 没关系, 我自己一个能找到他.:- 当然, 我会帮你.") == 1) {
			mes "[可疑的人]";
			mes "你能肯定...?";
			mes "红豹 乔是一个真正的";
			mes "伪装高手. 你会";
			mes "需要很多帮助,你才能";
			mes "找到他...";
			close;
		}
		mes "[可疑的人]";
		mes "很好,我很高兴";
		mes "听到这个. 请";
		mes "帮我找到";
		mes "^3355FF5 赛依伐^000000 和";
		mes "^3355FF1 强化武器金属-级数一^000000.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[可疑的人]";
			mes "请把";
			mes "^3355FF5 赛依伐^000000 和";
			mes "^3355FF1 强化武器金属-级数一^000000 带给我";
			mes "用你最快的速度. 那么,";
			mes "我能帮助你找到";
			mes "Red Leopard 乔.";
			close;
		}
		mes "[可疑的人]";
		mes "好, 好. 你已经";
		mes "带来了矿物质...";
		mes "现在, 轮到我";
		mes "帮助你了. 来这里,";
		mes "让我看一下那封信.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[可疑的人]";
		mes "为什么? 你怎么不带克尔加的信给我?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你是...";
		mes "你是野猫 乔?";
		next;
		mes "[可疑的人]";
		mes "...没错,但我比较喜欢";
		mes "被叫做红豹 乔.";
		mes "克尔加把你送给我, 对吧?";
		mes "他是唯一一个这样";
		mes "叫我的人. 你想成为";
		mes "一个忍者, 嗯? 哈,好吧.";
		next;
		mes "[红豹 乔]";
		mes "如果你想成为一个忍者,";
		mes "你必须一直注意";
		mes "自己所看到的和相信的.不要忘记";
		mes "保守秘密,那么你就是一名合格的忍者.";
		next;
		mes "[红豹 乔]";
		mes "记得行动要";
		mes "迅速, 和总要";
		mes "消失得无影无踪.";
		mes "继续隐藏在";
		mes "阴影中是我们真正的";
		mes "终极力量.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我知道了...";
		mes "...........";
		next;
		mes "[红豹 乔]";
		mes "那么现在, 让我看一下";
		mes "这封信. 我看一下...";
		mes "哈. 我想克尔加";
		mes "想再一次挑战我";
		mes "但他现在真的想要";
		mes "一个停战协议? 哈!";
		next;
		mes "[红豹 乔]";
		mes "谢谢你的帮助,";
		mes "我现在有矿物质了";
		mes "我需要制造一个苦无!";
		mes "哈哈哈! 当我有优势的时候我是不会同意停战的!";
		next;
		mes "[红豹 乔]";
		mes "无论怎样, 让我写一个";
		mes "答复给他. 我也会";
		mes "给你我的建议...";
		mes "我想你会成为一个非常出色的";
		mes "忍者, 尽管我之前骗了你";
		mes "哈哈哈!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[红豹 乔]";
		mes "就这样吧.";
		mes "请把这封信";
		mes "带给克尔加.";
		mes "我需要一段时间";
		mes "才回去Amatsu, 所以让";
		mes "直接帮你送到那里...";
		delitem 1010,1;	// 强化武器金属-级数一
		delitem 7053,5;	// 赛依伐
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[红豹 乔]";
		mes "额? 我不知道";
		mes "发生什么了, 但好像";
		mes "你没有把我的答复";
		mes "交给克尔加.";
		mes "要我现在直接送你";
		mes "去Amatsu吗?";
		next;
		if(select("不用, 谢谢.:好的, 麻烦了.") == 1) {
			mes "[红豹 乔]";
			mes "很好. 好的, 我只是";
			mes "想帮你节约";
			mes "一点时间.";
			close;
		}
		mes "[红豹 乔]";
		mes "好的, 那么.";
		mes "再见了.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[红豹 乔]";
		mes "克尔加也要求你";
		mes "帮他收集一些材料?";
		mes "哦,好吧, 我想";
		mes "我不能怪他.";
		mes "此外, 我应该可以";
		mes "在公平的决斗中打败他~";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[红豹 乔]";
		mes "啊, 你已经是一名忍者了~";
		mes "我希望你继续";
		mes "训练自己和尽力";
		mes "掌握所有的忍者技能";
		mes "永远记得";
		mes "要隐藏在阴影之中.";
		close;
	}
	else {
		mes "[游客]";
		mes "我曾经去过很多";
		mes "国家, 但我从来没有";
		mes "去过一个像艾音布罗克塔这么高";
		mes "的建筑.所有";
		mes "在我家乡的建筑";
		mes "都比这里的要渺小...";
		close;
	}
}
