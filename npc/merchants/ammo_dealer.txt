//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script =======================================
//= Bullet Dealers
//===== By ===================================================
//= Playtester, Paradox924X
//===== Version ==============================================
//= 2.1
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= Bullet trader.
//===== Comments =============================================
//= 1.0 First version [Playtester]
//= 1.1 Converted from Aegis [Paradox924X]
//= 1.2 More optimized conversion [Legionaire]
//= 1.2a Removed .GATs [Lupus]
//= 1.3 Fixed [Playtester] Optimized. Got rid of @vars [Lupus]
//= 1.4 Updated to match AEGIS script. [Kisuka]
//= 1.5 Updated to match AEGIS script again. [Masao]
//= 1.6 Moved Izlude duplicate to pre-re/re paths. [Euphy]
//= 2.0 Clean-up. [Capuche]
//= 2.1 Fixes Issue #1482, where Rebellion jobs cannot use this npc. [Limestone]
//============================================================

alberta,118,160,3	script	子弹商人 汤尼#alb::bdt	4_M_04,{
	mes "[汤尼]";
	if (BaseClass == Job_Gunslinger) {
		mes "我是汤尼, 一名弹药经销商. ";
		mes "当你缺乏弹药的时候, 可以来找我. ";
		mes "只要带给我材料, 我就能帮你制作出你所需的弹药. ";
		next;
		switch(select("毒物子弹:火焰子弹:雷击子弹:暗盲子弹:冰冻子弹:取消")) {
		case 1: callsub S_BulletTrade,937,10,13205;// <item required>, <amount req>, <bullet id>
		case 2: callsub S_BulletTrade,7097,2,13203;
		case 3: callsub S_BulletTrade,7053,3,13204;
		case 4: callsub S_BulletTrade,1024,5,13206;
		case 5: callsub S_BulletTrade,7054,2,13207;
		case 6:
			mes "[汤尼]";
			mes "改变主意啦？";
			mes "好吧, 我会一直呆在这里. ";
			mes "如果你需要弹药的话, 可以随时来找我. ";
			close;
		}
	}
	mes "嗨, 我叫汤尼. ";
	mes "我是专门为枪手们提供弹药货品的商人. ";
	next;
	mes "[汤尼]";
	mes "如果你想购买弹药的话, 我很遗憾. ";
	mes "我只能将弹药卖给熟练的枪手. ";
	close;

S_BulletTrade:
	.@required_id = getarg(0);
	.@required_amount = getarg(1);
	.@bullet_id = getarg(2);

	mes "[汤尼]";
	mes "想制作 30 枚 "+getitemname(.@bullet_id)+" 的话, 你得带来以下材料：";
	mes "强化武器金属-级数一 x 1";
	mes "块强化武器金属-级数二 x 1";
	mes getitemname(.@required_id) + " x " + .@required_amount +".";
	next;
	mes "[汤尼]";
	mes "记住一次最多交易500个30枚装的子弹. ";
	mes "如果不想兑换, 就输入 0 好了. ";
	next;
	input .@amount;
	mes "[汤尼]";
	if (.@amount < 1) {
		mes "看来你是不想兑换啊, 那么交易取消. ";
	}
	else if (.@amount > 500) {
		mes "嗨, 你想要的子弹太多了. ";
		mes "之前不是和你说过一次最多只能交易500个30枚装的子弹吗？";
		mes "除非...你想把你的装备也做成子弹...";
	}
	else if (countitem(1010) >= .@amount && countitem(1011) >= .@amount && countitem(.@required_id) >= (.@amount*.@required_amount)) {
		if (checkweight(.@bullet_id,.@amount * 30) == 0) {
			mes "嗯？你的背包没有空间可以放这些子弹. ";
			mes "等你将背包腾出足够的空间后, 再来找我吧. ";
			mes "试试看将一些东西放到卡普拉仓库. ";
		} else {
			mes "很好, 一切准备就绪. ";
			mes "材料我就带走了, 这是你要的子弹. ";
			mes "很高兴能和你做交易~";
			delitem 1010,.@amount; //Phracon
			delitem 1011,.@amount; //Emveretarcon
			delitem .@required_id,.@amount * .@required_amount;
			getitem .@bullet_id,.@amount * 30;
		}
	} else {
		mes "啧啧啧啧~";
		mes "你没有准备足够的材料. ";
		mes "不过没关系, 我不会用子弹射杀你的, ";
		mes "等你收集齐足够的材料后再来找我吧. ";
	}
	close;
}
