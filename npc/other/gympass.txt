//===== rAthenaCN 汉化声明 ===================================
//= 您可以随意转载和使用本汉化脚本。若您使用、借鉴或复制了
//= 此脚本中的汉化成果，那么请在您的脚本中加上并保留这七行
//= 汉化声明，由衷感谢。
//= 若您对这个脚本有更好的翻译建议，或发现此脚本的汉化错误，
//= 请前往 http://rathena.cn 上报您发现的问题，谢谢。
//============================================================

//===== rAthena Script ======================================= 
//= Gym Pass Cash Item NPC
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Exchange Gym Passes to learn "Enlarge Weight Limit"
//= iRO NPC situated in Payon beside Kafra Shop.
//===== Additional Comments: ================================= 
//= 1.0 First version [L0ne_W0lf]
//= 1.1 Fixed the NPC saying you "00" [L0ne_W0lf]
//= 1.2 Replaced effect numerics with constants. [Samuray22]
//= 1.3 Updated script, and fixed potential errors. [L0ne_W0lf]
//============================================================ 

payon,173,141,4	script	重力圣人#GymPass	899,{
	mes "[重力圣人]";
	mes "你好, 我是重力圣人.";
	mes "现代人的体质已经大不如前了,";
	mes "即使你是靠体力劳动维持生计的人,";
	mes "某些地方的肌肉还是会变得虚弱和松弛.";
	mes "知道我的意思吗??";
	next;
	mes "[重力圣人]";
	mes "跟我一起锻炼吧!";
	mes "我保证你可以拿更多的东西!";
	mes "每次只要给我一张^FF0000健身房会员券^000000";
	mes "我们就可以开始啦!";
	next;
	mes "[重力圣人]";
	mes "但是别太激动喔:";
	mes "虽然我可以陪你进行多次的锻炼,";
	mes "但是如果锻炼强度太大的话,";
	mes "你的身体会因为吃不消而累垮的哟.";
	mes "听明白了吗?";
	next;
	mes "[重力圣人]";
	mes "在这里你可以受到一系列安全的训练,";
	mes "这些训练可以帮你增加负重能力,";
	mes "每人最多可以参加 ^FF000010 次^000000 负重训练.";
	mes "准备好了吗?";
	next;
	switch(select("我准备好啦:我还没有准备好:呃, 我的负重能力出问题了.")) {
	case 1:
		if (gympassmemory < 10) {
			set .@add_carry,gympassmemory + 1;
			set .@remain_carry,10 - .@add_carry;
			if (countitem(7776) > 0) {
				mes "[重力圣人]";
				mes "喔, 我看到你带着健身券来了.";
				mes "那么现在开始, 请跟着我做,";
				mes "并试着去体会能量燃烧的感觉吧!";
				mes "预备? 开始!";
				next;
				specialeffect2 EF_EARTHSPIKE;
				next;
				specialeffect2 EF_DEVIL;
				next;
				specialeffect2 EF_COIN;
				next;
				specialeffect2 EF_SIGHTRASHER;
				next;
				mes "[重力圣人]";
				mes "好啦, 你的负重能力已经得到了提升!";
				mes "让我看看, 如果继续做这套训练的话,";
				mes "你的负重能力还能提升 ^FF00000" + .@remain_carry + "^000000 次!";
				delitem 7776,1; //Max_Weight_Up_Scroll
				set gympassmemory,.@add_carry;
				skill "ALL_INCCARRY",.@add_carry,SKILL_PERM_GRANT;
				close;
			}
			else {
				mes "[重力圣人]";
				mes "伙计, 忘记我之前告诉你的啦?";
				mes "如果你想增强体质提高负重能力的话,";
				mes "记得带^FF0000健身房会员券^000000过来!";
				mes "跟着我锻炼, 可以让你更加健美哦.";
				close;
			}
		}
		else {
			mes "[重力圣人]";
			mes "我想你的身体已经非常健美了!";
			mes "如果再继续锻炼的话,";
			mes "对你的身体来说是相当危险的!";
			mes "好吧, 别这样啦~~";
			mes "我曾告诉过你训练是有限制的!";
			close;
		}
	case 2:
		mes "[重力圣人]";
		mes "喔, 那真是太糟糕了.";
		mes "如果你改变主意的话, 欢迎随时回来.";
		mes "请推荐你的朋友来找我:";
		mes "如果需要锻炼身体增加负重能力的话, 我可以让他变得健美.";
		close;
	case 3:
		if (gympassmemory > 0) {
			mes "[重力圣人]";
			mes "发生什么事情了?";
			mes "你一定是经历了什么事情, 导致负重肌肉群萎缩了吧?";
			mes "不过幸运的是, 肌肉是有记忆功能的.";
			mes "过一段时间, 就可以恢复正常...";
			next;
			specialeffect2 EF_EARTHSPIKE;
			next;
			specialeffect2 EF_DEVIL;
			next;
			specialeffect2 EF_COIN;
			next;
			specialeffect2 EF_SIGHTRASHER;
			next;
			mes "[重力圣人]";
			mes "现在感觉怎么样?";
			mes "你的负重肌肉群应该恢复正常了!";
			skill "ALL_INCCARRY",gympassmemory,SKILL_PERM_GRANT;
			close;
		}
		else {
			mes "[重力圣人]";
			mes "呃...";
			mes "我们之前并没有一起锻炼过.";
			mes "我对此非常确定.";
			close;
		}
	}
}
